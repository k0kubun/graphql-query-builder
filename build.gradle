apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'com.github.k0kubun'
archivesBaseName = 'graphql-query-builder'
version = '0.4.0'
description = 'GraphQL query builder for Java'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:29.0-jre'
    testCompile 'junit:junit:4.12'
}

def deployUsername = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
def deployPassword = project.hasProperty('sonatypePassword') ? sonatypePassword : ''

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            pom.project {
                name 'graphql-query-builder'
                description 'GraphQL query builder for Java'
                packaging 'jar'
                url 'https://github.com/k0kubun/graphql-query-builder'
                scm {
                    connection 'scm:git:git://github.com/k0kubun/graphql-query-builder.git'
                    developerConnection 'scm:git@github.com:k0kubun/graphql-query-builder.git'
                    url 'https://github.com/k0kubun/graphql-query-builder'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'k0kubun'
                        name 'Takashi Kokubun'
                        email 'takashikkbn@gmail.com'
                    }
                }
            }
        }
    }
}
